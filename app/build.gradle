apply plugin: 'com.android.application'
apply from: 'copyLibs.gradle'    // enable 'copyLibs.gradle' script plugin
apply plugin: 'eclipse'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.0'
    defaultConfig {
        applicationId "com.zeapo.pwdstore"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 2001
        versionName "2.0.0-dev"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    // avoid Travis failures
    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude '.readme'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    //
    // To sign release builds, create the file `gradle.properties` in
    // $HOME/.gradle or in your project directory with this content:
    //
    // mStoreFile=/path/to/key.store
    // mStorePassword=xxx
    // mKeyAlias=alias
    // mKeyPassword=xxx
    //
    if (project.hasProperty('mStoreFile') &&
            project.hasProperty('mStorePassword') &&
            project.hasProperty('mKeyAlias') &&
            project.hasProperty('mKeyPassword')) {
        signingConfigs {
            release {
                storeFile file(mStoreFile)
                    storePassword mStorePassword
                    keyAlias mKeyAlias
                    keyPassword mKeyPassword
            }
        }
        buildTypes.release.signingConfig = signingConfigs.release
    } else {
        buildTypes.release.signingConfig = null
    }
}

ext.support_version = '25.0.0'

dependencies {
    compile "com.android.support:appcompat-v7:$support_version"
    compile "com.android.support:recyclerview-v7:$support_version"
    compile "com.android.support:cardview-v7:$support_version"
    compile "com.android.support:design:$support_version"
    compile "com.android.support:support-annotations:$support_version"
    compile 'org.sufficientlysecure:openpgp-api:11.0'
    compile 'com.nononsenseapps:filepicker:2.4.2'
    compile('org.eclipse.jgit:org.eclipse.jgit:3.7.1.201504261725-r') {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    compile 'com.jcraft:jsch:0.1.54'
    compile 'org.apache.commons:commons-io:1.3.2'

    androidTestCompile ('com.android.support.test:runner:0.5') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile ('com.android.support.test:rules:0.5') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile ('com.android.support.test.espresso:espresso-core:2.2.2') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}
